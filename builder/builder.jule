// Copyright 2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "doc"
use "std/bytes"
use "std/jule/ast"
use "std/strings"
use "std/unicode"
use "std/unicode/utf8"

struct metaHashStruct {
	ident: str
	meta:  &doc::MetaStruct
}

// Builds documentation from source code AST.
struct Builder {
	mut fmt: &formatter

	mut structMetas: []metaHashStruct
}

impl Builder {
	// Formats node and returns formatted code (including documentation comments).
	fn formatNode(self, mut node: ast::Node): str {
		self.fmt.buf.Clear()
		self.fmt.node(node)
		ret strings::TrimSpace(self.fmt.buf.Str())
	}

	// Formats type and returns formatted code.
	fn formatType(self, mut t: &ast::Type): str {
		self.fmt.buf.Clear()
		self.fmt.formatType(t)
		ret strings::TrimSpace(self.fmt.buf.Str())
	}

	// Builds context from doc comments.
	fn buildCtx(self, doc: str): []doc::Context {
		ctxb := ctxBuilder{}
		ret ctxb.build(doc)
	}

	// Reads documentation comments from code and returns meta.
	// Also returns code with no documentation comments.
	fn readDocComments(self, mut code: str): (str, []doc::Context) {
		// Read doc comments.
		mut doc := strings::Builder{}
		for len(code) > 0 {
			mut i := strings::IndexByte(code, '\n')
			if i == -1 {
				break
			}
			// Comments separated by empty new-line.
			// Reset doc comments.
			if i == 0 {
				doc.Clear()
				code = code[i+1:]
				continue
			}
			i++
			if i >= len(code) {
				break
			}
			if len(code) >= 2 && code[:2] == "//" {
				doc.WriteStr(code[:i])!
			} else {
				break
			}
			code = code[i:]
		}
		mut ctx := self.buildCtx(strings::TrimSpace(doc.Str()))
		ret code, ctx
	}

	fn formatMainNode(self, mut ast: &ast::AST, mut &i: int): str {
		mut data, ok := ast.Nodes[i].Data.(&ast::Var)
		if !ok {
			ret self.formatNode(ast.Nodes[i])
		}
		// For variables, do not use plain node formatting.
		// Variables may be grouped with a common documentation.
		// Do not waste it.
		self.fmt.buf.Clear()
		{
			mut row := ast.Nodes[i].Token.Row
			if len(data.Directives) > 0 {
				row = data.Directives[0].Tag.Row
			}
			if row-self.fmt.writeCommentsExcept(row, true) > 1 {
				self.fmt.write("\n")
			}
		}
		self.fmt.groupDecls[&ast::Var, ast::Node](ast.Nodes, i, fn(mut &d: &ast::Var) {
			unsafe {
				if !d.Binded && isPub(d.Ident) {
					self.fmt.buf.Write(self.fmt.indent)!
					self.fmt.varDecl(d, true)
				}
			}
		})
		// If variable is not grouped, the build function will advance the offset,
		// avoid false offset.
		if len(data.Group) == 0 {
			i--
		}
		ret strings::TrimSpace(self.fmt.buf.Str())
	}

	fn handleNodeDoc(self, node: ast::Node, mut doc: &doc::Doc): &doc::Doc {
		match type node.Data {
		| &ast::Var:
			// NOTE:
			// We cannot know the exact type of the variable without semantic analysis.
			// For this reason, we have to format variables with initialization expressions.
			// The initialization expressions will be useful for that.
			// But hidden types may be not useful, for example "[foo]".
			// In the example expression, "foo" is a private value, so it is not documented.
			// Reader cannot know the type of the "foo". The code writer should
			// be consider cases like this and should write type declaration.
			v := node.Data.(&ast::Var)
			if v.Binded {
				ret nil
			}
			doc.Name = v.Ident
			doc.Owner = doc::Var
		| &ast::Func:
			// NOTE:
			// We do not have to check whether this function is a test function.
			// According to Jule's test function rules, a test function never can be public.
			// So we will not document test functions.
			f := node.Data.(&ast::Func)
			if f.Binded {
				ret nil
			}
			doc.Name = f.Ident
			doc.Owner = doc::Func
		| &ast::Struct:
			s := node.Data.(&ast::Struct)
			if s.Binded {
				ret nil
			}
			doc.Name = s.Ident
			doc.Owner = doc::Struct
		| &ast::Trait:
			t := node.Data.(&ast::Trait)
			doc.Name = t.Ident
			doc.Owner = doc::Trait
		| &ast::Enum:
			e := node.Data.(&ast::Enum)
			doc.Name = e.Ident
			doc.Owner = doc::Enum
		| &ast::TypeEnum:
			e := node.Data.(&ast::TypeEnum)
			doc.Name = e.Ident
			doc.Owner = doc::TypeEnum
		| &ast::TypeAlias:
			a := node.Data.(&ast::TypeAlias)
			if a.Binded {
				ret nil
			}
			doc.Name = a.Ident
			if a.Strict {
				doc.Owner = doc::StrictTypeAlias
			} else {
				doc.Owner = doc::TypeAlias
			}
		| &ast::Impl:
			// no-op, handled as special case
		}
		ret doc
	}

	fn mainNode(self, mut ast: &ast::AST, mut &i: int): &doc::Doc {
		mut doc := new(doc::Doc)
		code, mut ctx := self.readDocComments(self.formatMainNode(ast, i))
		doc.Code = code
		doc.Ctx = ctx
		node := ast.Nodes[i]
		ret self.handleNodeDoc(node, doc)
	}

	fn node(self, mut node: ast::Node): &doc::Doc {
		mut doc := new(doc::Doc)
		code, mut ctx := self.readDocComments(self.formatNode(node))
		doc.Code = code
		doc.Ctx = ctx
		ret self.handleNodeDoc(node, doc)
	}

	// Returns MetaStruct by ident.
	// If not exist already, appends new MetaStruct by ident and returns it.
	fn getStructMeta(self, ident: str): &doc::MetaStruct {
		for (_, mut hash) in self.structMetas {
			if hash.ident == ident {
				ret hash.meta
			}
		}
		mut meta := new(doc::MetaStruct)
		self.structMetas = append(self.structMetas, metaHashStruct{
			ident: ident,
			meta: meta,
		})
		ret meta
	}

	fn analysisImpl(self, mut ast: &ast::Impl) {
		// Destination type should be simple identifier type.
		// Otherwise we have to assume this implementation is not suitable to analysis.
		match type ast.Dest.Kind {
		| &ast::IdentType:
			break
		|:
			ret
		}
		dest := ast.Dest.Kind.(&ast::IdentType)
		// Destination is not public. Skip.
		if !isPub(dest.Ident) {
			ret
		}
		mut meta := self.getStructMeta(dest.Ident)
		// If it is trait implementation, we have to check identifier type for
		// the trait type declaration for implementation. If identifier type is
		// not public, no need for documentation.
		if ast.IsTraitImpl() {
			name, isName := ast.Base.Kind.(&ast::IdentType)
			if isName && !isPub(name.Ident) {
				ret
			}
			meta.Traits = append(meta.Traits, self.formatType(ast.Base))
		}
		for (_, mut m) in ast.Methods {
			if isPub(m.Ident) {
				mut node := ast::Node{
					Token: m.Token,
					Data: m,
				}
				meta.Methods = append(meta.Methods, self.node(node))
			}
		}
	}

	// Dispatches all collected struct meta.
	fn dispatchStructMeta(self, mut doc: []&doc::Doc) {
		for (_, mut hash) in self.structMetas {
			for (_, mut node) in doc {
				if (node.Owner == doc::Struct || node.Owner == doc::StrictTypeAlias) &&
					node.Name == hash.ident {
					node.Meta = hash.meta
				}
			}
		}
	}

	// Dispatches all collected meta.
	fn DispatchMeta(self, mut doc: []&doc::Doc) {
		self.dispatchStructMeta(doc)
	}

	// Builds and returns documentation.
	// Will not dispatch meta.
	fn BuildPartial(self, mut ast: &ast::AST, mut cm: &CommentMap): []&doc::Doc {
		if ast == nil {
			ret nil
		}
		self.fmt.cm = cm
		mut doc := make([]&doc::Doc, 0)
		mut i := 0
		for i < len(ast.Nodes); i++ {
			mut node := ast.Nodes[i]
			// Special cases.
			match type node.Data {
			| &ast::Impl:
				self.analysisImpl(node.Data.(&ast::Impl))
				continue
			|:
				// no-op
			}
			mut nodeDoc := self.mainNode(ast, i)
			if nodeDoc == nil {
				continue
			}
			// Do not add exported defines.
			if !isPub(nodeDoc.Name) {
				continue
			}
			doc = append(doc, nodeDoc)
		}
		ret doc
	}

	// Builds and returns documentation with dispatched meta.
	fn Build(self, mut ast: &ast::AST, mut cm: &CommentMap): []&doc::Doc {
		mut doc := self.BuildPartial(ast, cm)
		self.DispatchMeta(doc)
		ret doc
	}
}

// Returns new Builder.
fn New(): &Builder {
	ret &Builder{
		fmt: formatter.new(),
	}
}

// Reports whether identifier is public.
// Derived from julec. Stay up to date.
fn isPub(ident: str): bool {
	if len(ident) == 0 {
		ret false
	}
	if ident[0] < utf8::RuneSelf { // ASCII, fast way.
		b := ident[0]
		ret 'A' <= b && b <= 'Z'
	}
	r, _ := utf8::DecodeRuneStr(ident)
	ret unicode::IsUpper(r)
}